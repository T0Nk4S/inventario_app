// Patched build.gradle (copiado desde pub cache y ajustado)
// Original path: %LOCALAPPDATA%\Pub\Cache\hosted\pub.dev\firebase_auth-6.1.1\android\build.gradle

group 'io.flutter.plugins.firebase.auth'
version '1.0-SNAPSHOT'

// Nota: en el pub-cache original reemplac√© throws GradleException por RuntimeException

apply plugin: 'com.android.library'

buildscript {
    repositories {
        google()
        mavenCentral()
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

def firebaseCoreProject = findProject(':firebase_core')
if (firebaseCoreProject == null) {
    throw new RuntimeException('Could not find the firebase_core FlutterFire plugin, have you added it as a dependency in your pubspec?')
} else if (!firebaseCoreProject.properties['FirebaseSDKVersion']) {
    throw new RuntimeException('A newer version of the firebase_core FlutterFire plugin is required, please update your firebase_core pubspec dependency.')
}

def getRootProjectExtOrCoreProperty(name, firebaseCoreProject) {
    if (!rootProject.ext.has('FlutterFire')) return firebaseCoreProject.properties[name]
    if (!rootProject.ext.get('FlutterFire')[name]) return firebaseCoreProject.properties[name]
    return rootProject.ext.get('FlutterFire').get(name)
}

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
      namespace 'io.flutter.plugins.firebase.auth'
    }

    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 23
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.toVersion(17)
        targetCompatibility JavaVersion.toVersion(17)
    }

    buildFeatures {
        buildConfig true
    }

    lintOptions {
        disable 'InvalidPackage'
    }
    dependencies {
        api firebaseCoreProject
        implementation platform("com.google.firebase:firebase-bom:${getRootProjectExtOrCoreProperty("FirebaseSDKVersion", firebaseCoreProject)}")
        implementation 'com.google.firebase:firebase-auth'
        implementation 'androidx.annotation:annotation:1.7.0'
    }
}

apply from: file("./user-agent.gradle")
